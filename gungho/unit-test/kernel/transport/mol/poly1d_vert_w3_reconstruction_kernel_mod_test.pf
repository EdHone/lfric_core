!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the reconstruction computation using 1d polynomials
!> (valid for lowest order quadrilateral elements)
module poly1d_vert_w3_reconstruction_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: poly1d_vert_w3_reconstruction_test_type
    private
  contains
    procedure test_all
  end type poly1d_vert_w3_reconstruction_test_type

contains

  @Test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env, only: real64
    use poly1d_vert_w3_reconstruction_kernel_mod,   only: poly1d_vert_w3_reconstruction_code

    use get_unit_test_planar_mesh_mod, &
                                     only: get_outward_normals_to_vertical_faces

    implicit none

    class(poly1d_vert_w3_reconstruction_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def  ! r_def 64bit tolerance
    real(r_def)            :: answer, use_tol

    integer(i_def), parameter :: nfaces_v = 2
    integer(i_def), parameter :: nfaces_h = 4
    integer(i_def), parameter :: nlayers = 3
    integer(i_def), parameter :: mol_order = 2
    integer(i_def), parameter :: ndata = (mol_order+1)*nfaces_v
    integer(i_def), parameter :: ndf_w2 = 6
    integer(i_def), parameter :: ndf_w3 = 1
    integer(i_def), parameter :: ndf_c  = 1
    integer(i_def), parameter :: undf_w2 = nfaces_h*nlayers + (nlayers+1)
    integer(i_def), parameter :: undf_w3 = ndf_w3*nlayers
    integer(i_def), parameter :: undf_c  = ndata*nlayers

    integer(i_def), dimension(ndf_w2) :: map_w2
    integer(i_def), dimension(ndf_w3) :: map_w3
    integer(i_def), dimension(ndf_c)  :: map_c

    real(r_def), dimension(3,ndf_w2,ndf_w2)  :: basis_w2
    real(r_def), allocatable, dimension(:,:) :: outward_normals_to_vertical_faces

    real(r_def), dimension(undf_w2) :: reconstruction
    real(r_def), dimension(undf_w2) :: wind
    real(r_def), dimension(undf_w3) :: density
    real(r_def), dimension(undf_c)  :: coeff

    integer(i_def) :: df, k, f
    integer(i_def) :: logspace

    map_w3(1) = 1
    map_c(1) = 1
    do df = 1,nfaces_h
      map_w2(df) = 1 + (df-1)*nlayers
    end do
    map_w2(5) = 1 + nfaces_h*nlayers
    map_w2(6) = map_w2(5) + 1

    ! Get canned information for the reference cube
    call get_outward_normals_to_vertical_faces(outward_normals_to_vertical_faces)

    wind(:) = 1.0_r_def

    basis_w2 = 0.0_r_def
    basis_w2(1,1,1) = 1.0_r_def
    basis_w2(2,2,2) = -1.0_r_def
    basis_w2(1,3,3) = 1.0_r_def
    basis_w2(2,4,4) = -1.0_r_def
    basis_w2(3,5,5) = 1.0_r_def
    basis_w2(3,6,6) = 1.0_r_def

    do k = 1,nlayers
      density(k) = real(k,r_def) - 0.5_r_def
    end do

    ! Quadratic fit for most of the domain
    do k = 1, nlayers-2
      coeff( 0 + k*ndata + map_c(1)) =  2.0_r_def/6.0_r_def
      coeff( 1 + k*ndata + map_c(1)) =  5.0_r_def/6.0_r_def
      coeff( 2 + k*ndata + map_c(1)) = -1.0_r_def/6.0_r_def
      coeff( 0 + (mol_order+1) + k*ndata + map_c(1)) = -1.0_r_def/6.0_r_def
      coeff( 1 + (mol_order+1) + k*ndata + map_c(1)) =  5.0_r_def/6.0_r_def
      coeff( 2 + (mol_order+1) + k*ndata + map_c(1)) =  2.0_r_def/6.0_r_def
    end do
    ! Linear fit on boundaries
    k = nlayers-1
    do f = 0, 1
      coeff( 0 + f*(mol_order+1) + map_c(1)) =  0.5_r_def
      coeff( 1 + f*(mol_order+1) + map_c(1)) =  0.5_r_def
      coeff( 2 + f*(mol_order+1) + map_c(1)) =  0.0_r_def
      coeff( 0 + f*(mol_order+1) + k*ndata + map_c(1)) =  0.5_r_def
      coeff( 1 + f*(mol_order+1) + k*ndata + map_c(1)) =  0.5_r_def
      coeff( 2 + f*(mol_order+1) + k*ndata + map_c(1)) =  0.0_r_def
    end do

    ! First test standard polynomial interpolation
    logspace = 0_i_def
    call poly1d_vert_w3_reconstruction_code( nlayers,                           &
                                             reconstruction,                    &
                                             wind,                              &
                                             density,                           &
                                             coeff,                             &
                                             ndata,                             &
                                             mol_order,                         &
                                             logspace,                          &
                                             ndf_w2,                            &
                                             undf_w2,                           &
                                             map_w2,                            &
                                             basis_w2,                          &
                                             ndf_w3,                            &
                                             undf_w3,                           &
                                             map_w3,                            &
                                             ndf_c,                             &
                                             undf_c,                            &
                                             map_c,                             &
                                             nfaces_v,                          &
                                             outward_normals_to_vertical_faces)
    answer = 1.0_r_def
    @assertEqual(answer, reconstruction(map_w2(5)+1) , tol)
    answer = 2.0_r_def
    @assertEqual(answer, reconstruction(map_w2(5)+2) , tol)

    ! Next test standard polynomial interpolation in log-space

    do k = 1,nlayers
      density(k) = exp(-real(k,r_def)+0.5_r_def)
    end do
    logspace = 1_i_def
    call poly1d_vert_w3_reconstruction_code( nlayers,                           &
                                             reconstruction,                    &
                                             wind,                              &
                                             density,                           &
                                             coeff,                             &
                                             ndata,                             &
                                             mol_order,                         &
                                             logspace,                          &
                                             ndf_w2,                            &
                                             undf_w2,                           &
                                             map_w2,                            &
                                             basis_w2,                          &
                                             ndf_w3,                            &
                                             undf_w3,                           &
                                             map_w3,                            &
                                             ndf_c,                             &
                                             undf_c,                            &
                                             map_c,                             &
                                             nfaces_v,                          &
                                             outward_normals_to_vertical_faces)
    answer = exp(-1.0_r_def)
    @assertEqual(answer, reconstruction(map_w2(5)+1), tol)
    answer = exp(-2.0_r_def)
    if ( r_def == real64 ) then
      use_tol = tol
      @assertEqual(answer, reconstruction(map_w2(5)+2), use_tol)
    else
      use_tol = 10.0_r_def*spacing( reconstruction(map_w2(5)+2) )
      @assertEqual(answer, reconstruction(map_w2(5)+2), use_tol)
    endif

  end subroutine test_all

end module poly1d_vert_w3_reconstruction_kernel_mod_test
